group 'dev.rexios.polar'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.5.21'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()

        // This is stupid, but local file dependencies weren't working
        ivy {
            url 'https://github.com/'

            patternLayout {
                artifact '/[organisation]/polar-ble-sdk/raw/[revision]/polar-sdk-android/libs/[module].[ext]'
            }

            // This is required in Gradle 6.0+ as metadata file (ivy.xml)
            // is mandatory. Docs linked below this code section
            metadataSources { artifact() }
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 21
        consumerProguardFiles 'proguard-rules.pro'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Dependencies for Polar SDK
    implementation 'polarofficial:polar-ble-sdk:3.2.6@aar'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.1'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'commons-io:commons-io:2.10.0'
    implementation 'com.google.protobuf:protobuf-javalite:3.17.3'

    implementation 'com.google.code.gson:gson:2.8.9'
}
